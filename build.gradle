defaultTasks 'publishToMavenLocal'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version '0.1'
group   'org.jacamo-lang'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()

    maven { url "http://jacamo.sourceforge.net/maven2" }

}

dependencies {
    compile group: 'org.jacamo', name: 'jacamo', version: '0.8'

	// implementation 'org.springframework.boot:spring-boot-starter-web'
	// testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation 'com.graphql-java:graphql-java-tools:5.2.4'
	implementation 'com.graphql-java:graphql-spring-boot-starter:5.0.2'
	implementation 'com.graphql-java:graphiql-spring-boot-starter:5.0.2'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

clean {
    delete 'bin'
    delete 'build'
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    baseName 'jacamo-graphql'
    classifier = 'javadoc'
    //from '${docsDir}/../../doc/api'
}

task sourceJar (type : Jar) {
    baseName 'jacamo-graphql'
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar, javadocJar
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId 'jacamo-graphql'
            artifact sourceJar
	          artifact javadocJar
        }
    }
}

task publishMavenGitHub(dependsOn: [ 'build', 'publishToMavenLocal']) {
  doLast {
    def wdir = System.getProperty("user.home")+'/.m2/repository/org/jacamo-lang/jacamo-graphql'
    def rdir = System.getProperty("user.home")+'/mavenpro/jacamo-mvn-repo'
    exec {
        commandLine 'git', 'pull'
        workingDir rdir
    }
    copy {
        from wdir
        into rdir + '/org/jacamo-lang/jacamo-graphql'
    }
    exec {
        commandLine 'git', 'add', '*'
        workingDir rdir
    }
    exec {
        commandLine 'git', 'commit', '-a', '-m', 'New version of jacamo-graphql: '+project.version
        workingDir rdir
    }
    exec {
        commandLine 'git', 'push'
        workingDir rdir
    }
  }
}
